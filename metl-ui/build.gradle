apply from: '../metl-assemble/common.gradle'

apply plugin: 'fi.jasoft.plugin.vaadin'

configurations {
    executableWar
}

eclipse {
    wtp {
        component {
            sourceDirs += file('src/test/resources')
        }
    }
}

war {
    archiveName="metl.war"
    filesMatching("**/*.properties") {
       filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [               
         'appVersion': appVersion, 
         'buildTime': new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new java.util.Date()), 
         'scmVersion' : gitVersion,
         'scmBranch' : gitBranch 
         ])
    }
    
    from {
        configurations.executableWar.collect {
            it.isDirectory() ? it : project.zipTree(it)
        }
    }    
    manifest { attributes 'Main-Class': 'org.jumpmind.metl.StartWebServer' }
}

vaadin.version = vaadinVersion
vaadin.widgetset = 'org.jumpmind.metl.ui.AppWidgetSet'
vaadin.manageWidgetset = true
vaadin.manageDependencies = true
vaadin.push = true

dependencies {
    compile project(":metl-core")
    compile group:"org.jumpmind.symmetric", name:"symmetric-ui-common", version:"1.0.10-SNAPSHOT", changing:true
    compile "com.cybercom:spring-ui-scope:0.0.2"
    compile "org.vaadin.addons:aceeditor:0.8.13"
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.peimari:maddon:1.20"
    compile "org.apache.poi:poi:3.11"
    compile "io.springfox:springfox-swagger2:$springfoxSwaggerVersion"
    compile "io.springfox:springfox-swagger-ui:$springfoxSwaggerVersion"
    compile "org.reficio:soap-builder:$reficioSoapVersion"
    compile "jlibs:jlibs-xml:1.0"
//    compile "javax.servlet:javax.servlet-api:$servletVersion"
    
//    testCompile "org.eclipse.jetty:jetty-webapp:$jettyVersion"
//    testCompile "org.eclipse.jetty:jetty-jmx:$jettyVersion"
//    testCompile "org.eclipse.jetty:jetty-annotations:$jettyVersion"
    
    executableWar project(":metl-server")
    
}

develop.dependsOn vaadinCompileWidgetset
develop.dependsOn generateDocs
assemble.dependsOn generateDocs
